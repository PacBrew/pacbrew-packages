# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-binutils
pkgver=2.36.1
pkgrel=1
pkgdesc='Binutils for ps4'
arch=('any')
url='https://mirror.cyberbits.eu/gnu/binutils/'
license=('GNU GPL')
options=(!strip libtool)
source=("https://mirror.cyberbits.eu/gnu/binutils/binutils-$pkgver.tar.bz2")
sha256sums=('5b4bd2e79e30ce8db0abd76dd2c2eae14a94ce212cfc59d3c37d23e24bc6d7a3')
groups=('ps4-dev')

build() {
  cd binutils-$pkgver
  
  ./configure --prefix=/opt/pacbrew/ps4/toolchain \
    --target=x86_64-pc-freebsd9 \
    --program-prefix=orbis- \
    --quiet \
    --disable-nls \
    --disable-dependency-tracking \
    --disable-werror \
    --enable-ld \
    --enable-lto \
    --enable-plugins \
    --enable-poison-system-directories \
    --with-sysroot=/

  make --quiet -j $(getconf _NPROCESSORS_ONLN)
}

package() {
  cd binutils-$pkgver
  make DESTDIR="${pkgdir}" install-strip
}
