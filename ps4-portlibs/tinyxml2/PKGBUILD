# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-tinyxml2
pkgver=9.0.0
pkgrel=1
pkgdesc='A simple, small, efficient, C++ XML parser that can be easily integrated into other programs'
arch=('any')
url='http://leethomason.github.io/tinyxml2/'
license=('apache')
options=(!strip libtool staticlibs)
source=( "https://github.com/leethomason/tinyxml2/archive/${pkgver}.tar.gz" )
makedepends=('ps4-pkg-config' 'ps4-toolchain-vars')
sha256sums=('cc2f1417c308b1f6acc54f88eb70771a0bf65f76282ce5c40e54cfe52952702c')
groups=('ps4-portlibs')

build() {
  cd tinyxml2-$pkgver

  source /opt/pacbrew/ps4/toolchain/ps4vars.sh

  mkdir build && cd build

  orbis-cmake -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=$ORBISDEV/usr \
    -DBUILD_TESTS=OFF -DBUILD_SHARED_LIBS=OFF -DBUILD_STATIC_LIBS=ON \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    ..

  make
}

package() {
  cd tinyxml2-$pkgver/build
  make DESTDIR="$pkgdir" install
}
