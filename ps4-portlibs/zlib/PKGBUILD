# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-zlib
pkgver=1.2.11
pkgrel=1
pkgdesc='Deflate compression method library'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
source=("https://zlib.net/zlib-$pkgver.tar.gz")
sha256sums=('c3e5e9fdd5004dcb542feda5ee4f0ff0744628baf8ed2dd5d66f8ca1197cb1a1')
makedepends=('ps4-toolchain-vars')
groups=('ps4-portlibs')

build() {
  source /opt/pacbrew/ps4/toolchain/ps4libvars.sh
  cd zlib-$pkgver
  
  # zlib
  ./configure --prefix="${PORTLIBS_PREFIX}" --static
  make libz.a # only build library, no programs

  # libminizip
  cd contrib/minizip
  autoreconf --force --verbose --install
  CFLAGS="${CFLAGS} -DUSE_FILE32API" \
    ./configure --prefix="${PORTLIBS_PREFIX}" --host=x86_64 --disable-shared --enable-static
  make
}

package() {
  source /opt/pacbrew/ps4/toolchain/ps4libvars.sh
  cd zlib-$pkgver
  # zlib
  make DESTDIR="$pkgdir" install
  # libminizip
  cd contrib/minizip
  make DESTDIR="$pkgdir" install
}
