# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-libcxx
pkgver=13.0.0
pkgrel=1
pkgdesc='Clang for ps4'
arch=('any')
url='https://github.com/llvm/llvm-project/'
license=('GNU GPL')
options=(!strip libtool)
source=("https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/llvm-project-$pkgver.src.tar.xz" "llvm.patch")
sha256sums=(
  '6075ad30f1ac0e15f07c1bf062c1e1268c241d674f11bd32cdf0e040c71f2bf3'
  '1568708df697ee3ed954fca447c49c217a7aba1e1f227fef6e0fc1da43d49dcc'
)
groups=('ps4-toolchain')

build() {
  cd llvm-project-$pkgver.src
  
  patch -p1 -i $srcdir/llvm.patch
  
  ########################
  # libunwind
  ########################
  
  cd libunwind && mkdir build && cd build

  cmake \
    -DCMAKE_C_COMPILER=/opt/pacbrew/ps4/toolchain/bin/clang \
    -DCMAKE_CXX_COMPILER=/opt/pacbrew/ps4/toolchain/bin/clang++ \
    -DCMAKE_CXX_FLAGS=-D_BSD_SOURCE \
    -DLIBUNWIND_TARGET_TRIPLE=x86_64-scei-ps4 \
    -DLIBUNWIND_ENABLE_SHARED=OFF \
    -DLIBUNWIND_ENABLE_STATIC=ON \
    -DLIBUNWIND_USE_COMPILER_RT=ON \
    -DLIBUNWIND_SYSROOT=/opt/pacbrew/ps4/toolchain \
    -DCMAKE_INSTALL_PREFIX=/opt/pacbrew/ps4/toolchain/usr \
    -DLLVM_PATH=../../llvm \
    -G Ninja \
    ..
    
  cmake --build . -- -j $(getconf _NPROCESSORS_ONLN)
  
  ########################
  # libcxx
  ########################
  
  cd ../../libcxx && mkdir build && cd build
  
  cmake \
    -DCMAKE_C_COMPILER=/opt/pacbrew/ps4/toolchain/bin/clang \
    -DCMAKE_CXX_COMPILER=/opt/pacbrew/ps4/toolchain/bin/clang++ \
    -DCMAKE_CXX_FLAGS="-D_BSD_SOURCE -D__ORBIS__ -frtti -fexceptions" \
    -DORBIS=ON \
    -DLIBCXX_TARGET_TRIPLE=x86_64-scei-ps4 \
    -DLIBCXX_CXX_ABI=libcxxabi \
    -DLIBCXX_CXX_ABI_INCLUDE_PATHS=../../libcxxabi/include \
    -DLIBCXX_CXX_ABI_LIBRARY_PATH=../../libcxxabi/build/lib \
    -DLIBCXX_ENABLE_SHARED=OFF \
    -DLIBCXX_SYSROOT=/opt/pacbrew/ps4/toolchain \
    -DLIBCXX_ENABLE_STATIC_ABI_LIBRARY=OFF \
    -DLIBCXX_USE_COMPILER_RT=YES \
    -DCMAKE_INSTALL_PREFIX=/opt/pacbrew/ps4/toolchain/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLLVM_PATH=../../llvm \
    -G Ninja \
    ..
    
  cmake --build . -- -j $(getconf _NPROCESSORS_ONLN)
  
  ########################
  # libcxxabi
  ########################
  
  cd ../../libcxxabi && mkdir build && cd build
  
  cmake \
    -DCMAKE_C_COMPILER=/opt/pacbrew/ps4/toolchain/bin/clang \
    -DCMAKE_CXX_COMPILER=/opt/pacbrew/ps4/toolchain/bin/clang++ \
    -DCMAKE_CXX_FLAGS="-D_BSD_SOURCE -frtti -fexceptions " \
    -DLIBCXXABI_TARGET_TRIPLE=x86_64-scei-ps4 \
    -DCMAKE_SHARED_LINKER_FLAGS="-L../../libunwind/build/lib" \
    -DLIBCXXABI_USE_LLVM_UNWINDER=ON \
    -DLIBCXXABI_ENABLE_SHARED=OFF \
    -DLIBCXXABI_USE_COMPILER_RT=YES \
    -DLIBCXXABI_INCLUDE_TESTS=OFF \
    -DLIBCXXABI_LIBCXX_INCLUDES=../../libcxx/build/include/c++/v1 \
    -DLIBCXXABI_SYSROOT=/opt/pacbrew/ps4/toolchain \
    -DCMAKE_INSTALL_PREFIX=/opt/pacbrew/ps4/toolchain/usr \
    -DLLVM_PATH=../../llvm \
    -G Ninja \
    ..
    
  cmake --build . -- -j $(getconf _NPROCESSORS_ONLN)
}

package() {
  # libunwind
  cd llvm-project-$pkgver.src/libunwind/build
  DESTDIR=$pkgdir cmake --build . --target install
  
  # libcxx
  cd ../../libcxx/build
  DESTDIR=$pkgdir cmake --build . --target install
  
  # libcxxabi
  cd ../../libcxxabi/build
  DESTDIR=$pkgdir cmake --build . --target install
}
